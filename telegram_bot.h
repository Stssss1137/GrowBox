#ifndef TELEGRAM_BOT_H
#define TELEGRAM_BOT_H

#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <UniversalTelegramBot.h>

const char* WIFI_SSID = "XXXXXXX";
const char* WIFI_PASS = "XXXXXXX";
const char* BOT_TOKEN = "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"; 

String storedChatId = ""; 

extern float getAHTTemp();
extern float getAHTHum();
extern int getSoilPercent();
extern bool getPumpState();
extern bool isAutoMode();
extern int getSoilThreshold();
extern int getLightDuration();
extern String getModeName();
extern bool getLedState();
extern void manualPumpControl(bool on);
extern void setPlantMode(int mode);
extern void setCustomThreshold(int val);

WiFiClientSecure client;
UniversalTelegramBot bot(BOT_TOKEN, client);
unsigned long lastBotCheck = 0;
const unsigned long BOT_INTERVAL = 1000; 

void setupWiFiAndBot() {
  Serial.print(F("WiFi Connecting..."));
  WiFi.mode(WIFI_STA);
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  int attempt = 0;
  while (WiFi.status() != WL_CONNECTED) {
    delay(500); Serial.print(".");
    if (++attempt > 30) { ESP.restart(); }
  }
  Serial.println(F("\nConnected."));
  client.setInsecure();
}

void sendBotMessage(String msg) {
  if (storedChatId != "") {
    bot.sendMessage(storedChatId, msg);
  }
}

String getStatusMsg() {
  String m = "ðŸ“Š *STATUS*\n";
  m += "Mode: " + getModeName() + (isAutoMode()?" (AUTO)":" (MANUAL)") + "\n";
  m += "Temp: " + String(getAHTTemp(),1) + "C, Hum: " + String(getAHTHum(),0) + "%\n";
  m += "Soil: " + String(getSoilPercent()) + "% (Limit: " + String(getSoilThreshold()) + "%)\n";
  m += "Light Sched: " + String(getLightDuration()) + "h\n";
  m += "LED Actual: " + String(getLedState() ? "ON ðŸ’¡" : "OFF ðŸŒ‘") + "\n";
  m += "Pump: " + String(getPumpState()?"ON ðŸ’¦":"OFF");
  return m;
}

void handleNewMessages(int numNewMessages) {
  for (int i = 0; i < numNewMessages; i++) {
    String chatId = String(bot.messages[i].chat_id);
    String text = bot.messages[i].text;
    
    storedChatId = chatId; 

    if (text == "/start" || text.startsWith("/help")) {
      String help = "ðŸŒ¿ *GrowBox*\n"
                    "`/status` - Ð†Ð½Ñ„Ð¾\n"
                    "`/pump_on` - ÐŸÐ¾Ð»Ð¸Ñ‚Ð¸ (5 ÑÐµÐº)\n"
                    "`/pump_off` - Ð¡Ñ‚Ð¾Ð¿\n\n"
                    "âš™ï¸ *Ð ÐµÐ¶Ð¸Ð¼Ð¸ (Ð’Ð¼Ð¸ÐºÐ°Ñ” AUTO)*\n"
                    "`/mode_tropical` (60%, 12Ð³)\n"
                    "`/mode_succulent` (20%, 14Ð³)\n"
                    "`/mode_veg` (45%, 16Ð³)\n"
                    "`/set_limit 50` (Ð¡Ð²Ñ–Ð¹ %)";
      bot.sendMessage(chatId, help, "Markdown");
    }
    else if (text == "/status") {
      bot.sendMessage(chatId, getStatusMsg(), "Markdown");
    }
    else if (text == "/pump_on" || text == "/pump_pulse") {
      manualPumpControl(true);
      bot.sendMessage(chatId, "ðŸ’¦ ÐŸÐ¾Ð»Ð¸Ð² (5 ÑÐµÐº). ÐÐ²Ñ‚Ð¾-Ñ€ÐµÐ¶Ð¸Ð¼ Ð²Ð¸Ð¼ÐºÐ½ÐµÐ½Ð¾.");
    }
    else if (text == "/pump_off") {
      manualPumpControl(false);
      bot.sendMessage(chatId, "ðŸ›‘ ÐŸÐ¾Ð¼Ð¿Ð° ÑÑ‚Ð¾Ð¿.");
    }
    else if (text == "/mode_tropical") {
      setPlantMode(1);
      bot.sendMessage(chatId, "ðŸŒ´ Ð ÐµÐ¶Ð¸Ð¼: Ð¢Ñ€Ð¾Ð¿Ñ–ÐºÐ¸\nÐ“Ñ€Ð°Ñ„Ñ–Ðº ÑÐ²Ñ–Ñ‚Ð»Ð°: 12 Ð³Ð¾Ð´ (Ð¢Ñ–Ð»ÑŒÐºÐ¸ ÑÐºÑ‰Ð¾ Ñ‚ÐµÐ¼Ð½Ð¾)");
    }
    else if (text == "/mode_succulent") {
      setPlantMode(2);
      bot.sendMessage(chatId, "ðŸŒµ Ð ÐµÐ¶Ð¸Ð¼: Ð¡ÑƒÐºÑƒÐ»ÐµÐ½Ñ‚Ð¸\nÐ“Ñ€Ð°Ñ„Ñ–Ðº ÑÐ²Ñ–Ñ‚Ð»Ð°: 14 Ð³Ð¾Ð´ (Ð¢Ñ–Ð»ÑŒÐºÐ¸ ÑÐºÑ‰Ð¾ Ñ‚ÐµÐ¼Ð½Ð¾)");
    }
    else if (text == "/mode_veg") {
      setPlantMode(3);
      bot.sendMessage(chatId, "ðŸ… Ð ÐµÐ¶Ð¸Ð¼: ÐžÐ²Ð¾Ñ‡Ñ–\nÐ“Ñ€Ð°Ñ„Ñ–Ðº ÑÐ²Ñ–Ñ‚Ð»Ð°: 16 Ð³Ð¾Ð´ (Ð¢Ñ–Ð»ÑŒÐºÐ¸ ÑÐºÑ‰Ð¾ Ñ‚ÐµÐ¼Ð½Ð¾)");
    }
    else if (text.startsWith("/set_limit")) {
      int val = text.substring(11).toInt();
      if(val > 0) {
        setCustomThreshold(val);
        bot.sendMessage(chatId, "âš™ï¸ ÐŸÐ¾Ñ€Ñ–Ð³: " + String(val) + "%");
      } else {
        bot.sendMessage(chatId, "ÐŸÑ€Ð¸ÐºÐ»Ð°Ð´: /set_limit 60");
      }
    }
  }
}

void handleTelegram() {
  if (millis() - lastBotCheck > BOT_INTERVAL) {
    int n = bot.getUpdates(bot.last_message_received + 1);
    while (n) {
      handleNewMessages(n);
      n = bot.getUpdates(bot.last_message_received + 1);
    }
    lastBotCheck = millis();
  }
}
#endif
